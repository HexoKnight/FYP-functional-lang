use crate::reprs::ast::*;
use crate::common::WithInfo;

grammar();

extern {
    type Error = crate::parsing::UserParserError;
}

match {
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
} else {
    _
}

Ident: Ident<'input> = <r"[a-zA-Z]+"> => Ident { name: <> };

Box<T>: Box<T> = <T> => Box::new(<>);

WithInfo<T>: WithInfo<Span<'input>, T> = {
    <start:@L> <inner:T> <end:@R> => WithInfo(Span {
        text: &input[start..end],
        start,
    }, inner),
}

pub Term: Term<'input> = WithInfo<RawTerm>;

RawTerm: RawTerm<'input> = {
    #[precedence(level="2")]
    "\\" <arg:Ident> ":" <arg_type:Type> <body:Box<WithInfo<RawTerm>>> => RawTerm::Abs(Abs { arg, arg_type, body }),

    #[precedence(level="1")]
    #[assoc(side="left")]
    <func:Box<WithInfo<RawTerm>>> <arg:Box<WithInfo<RawTerm>>> => RawTerm::App(App { func, arg }),

    #[precedence(level="0")]
    SimpleTerm,
}

SimpleTerm: RawTerm<'input> = {
    // TODO: capture the inner info instead
    "(" <RawTerm> ")",

    <ident:Ident> =>? {
        Ok(RawTerm::Var(Var { ident }))
    },

    "true" => RawTerm::Bool(true),
    "false" => RawTerm::Bool(false),
}

Type: Type<'input> = WithInfo<RawType>;

RawType: RawType<'input> = {
    #[precedence(level="1")]
    #[assoc(side="right")]
    <arg:Box<WithInfo<RawType>>> "->" <result:Box<WithInfo<RawType>>> => RawType::Arr(Arr { arg, result }),

    #[precedence(level="0")]
    SimpleType,
}
SimpleType: RawType<'input> = {
    // TODO: capture the inner info instead
    "(" <RawType> ")",
    "bool" => RawType::Bool,
}
